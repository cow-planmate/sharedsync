plugins {
    id "io.spring.dependency-management" version "1.1.7" apply false
}

allprojects {
    group = "com.sharedsync"
    version = "1.1.8"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "io.spring.dependency-management"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:3.5.3")

        compileOnly "org.projectlombok:lombok:1.18.34"
        annotationProcessor "org.projectlombok:lombok:1.18.34"

        testCompileOnly "org.projectlombok:lombok:1.18.34"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.34"

        testImplementation "org.springframework.boot:spring-boot-starter-test"

    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/cow-planmate/sharedsync")

                credentials {
                    username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user")
                    password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
                }
            }
        }
    }
}
